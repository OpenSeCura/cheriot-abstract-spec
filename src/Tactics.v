(*
 * Copyright 2026 Google LLC
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *)

From Stdlib Require Import String List Lia Bool Nat NArith.

Tactic Notation "learn_hyp" constr(p) "as" ident(H') :=
  let P := type of p in
  match goal with
  | H : P |- _ => fail 1
  | _ => pose proof p as H'
  end.
Tactic Notation "learn_hyp" constr(p) :=
  let H := fresh in learn_hyp p as H.

Lemma simple_tuple_inversion:
  forall {A} {B} (a: A) (b: B) x y,
  (a,b) = (x,y) ->
  a = x /\ b = y.
Proof.
  intros. inversion H. auto.
Qed.

Tactic Notation "simplify_eq" := repeat
  match goal with
  | H : False |- _ => contradiction
  | H : ?x = _ |- _ => subst x
  | H: _ = ?x |- _ => subst x
  | [ H: (_,_) = (_,_) |- _ ] =>
    apply simple_tuple_inversion in H;
    let Hl := fresh H "l" in let Hr := fresh H "r" in destruct H as [Hl Hr]
  end.

Tactic Notation "inv" ident(H) :=
  inversion H; clear H; simplify_eq.

Ltac simpl_match :=
  let repl_match_goal d d' :=
      replace d with d';
      lazymatch goal with
      | [ |- context[match d' with _ => _ end] ] => fail
      | _ => idtac
      end in
  let repl_match_hyp H d d' :=
      replace d with d' in H;
      lazymatch type of H with
      | context[match d' with _ => _ end] => fail
      | _ => idtac
      end in
  match goal with
  | [ Heq: ?d = ?d' |- context[match ?d with _ => _ end] ] =>
      repl_match_goal d d'
  | [ Heq: ?d' = ?d |- context[match ?d with _ => _ end] ] =>
      repl_match_goal d d'
  | [ Heq: ?d = ?d', H: context[match ?d with _ => _ end] |- _ ] =>
      repl_match_hyp H d d'
  | [ Heq: ?d' = ?d, H: context[match ?d with _ => _ end] |- _ ] =>
      repl_match_hyp H d d'
  end.
Ltac destruct_products :=
  repeat match goal with
  | p: _ * _  |- _ => destruct p
  | H: _ /\ _ |- _ => let Hl := fresh H "l" in let Hr := fresh H "r" in destruct H as [Hl Hr]
  | E: exists y, _ |- _ => let yf := fresh y in destruct E as [yf E]
  | H: context[let '(_,_) := ?x in _] |- _ =>
    destruct x eqn:?
  | |- context[let '(_,_) := ?x in _] =>
    destruct x eqn:?
  end.

Ltac destruct_matches_in e :=
  lazymatch e with
  | context[match ?d with | _ => _ end] =>
      destruct_matches_in d
  | _ =>
      destruct e eqn:?
  end.

Ltac destruct_matches_in_hyp H :=
  lazymatch type of H with
  | context[match ?d with | _ => _ end] =>
      destruct_matches_in d
  | ?v =>
      let H1 := fresh H in
      destruct v eqn:H1
  end.

Tactic Notation "case_match" "eqn" ":" ident(Hd) :=
  match goal with
  | H : context [ match ?x with _ => _ end ] |- _ => destruct x eqn:Hd
  | |- context [ match ?x with _ => _ end ] => destruct x eqn:Hd
  end.
Ltac case_match :=
  let H := fresh in case_match eqn:H.
Ltac simplify_nats :=
  match goal with
  | H: Nat.eqb _ _ = true |- _ =>
      rewrite PeanoNat.Nat.eqb_eq in H
  | H: Nat.eqb _ _ = false |- _ =>
      rewrite PeanoNat.Nat.eqb_neq in H
  end.
Ltac fast_done :=
  solve
    [ eassumption
    | symmetry; eassumption
    | reflexivity ].
Tactic Notation "fast_by" tactic(tac) :=
  tac; fast_done.
Ltac done :=
  solve
  [ repeat first
    [ fast_done
    | solve [trivial]
    | progress intros
    | solve [symmetry; trivial]
    | discriminate
    | contradiction
    | split
    ]
  ].
Tactic Notation "by" tactic(tac) :=
  tac; done.
Tactic Notation "destruct_or" "?" ident(H) :=
  repeat match type of H with
  | False => destruct H
  | _ \/ _ => destruct H as [H|H]
  end.
Tactic Notation "destruct_or" "!" ident(H) := hnf in H; progress (destruct_or? H).

Tactic Notation "destruct_or" "?" :=
  repeat match goal with H : _ |- _ => progress (destruct_or? H) end.
Tactic Notation "destruct_or" "!" :=
  progress destruct_or?.
Tactic Notation "split_and" :=
  match goal with
  | |- _/\ _ => split
  end.
Tactic Notation "split_and" "?" := repeat split_and.
Ltac split_ands := repeat split_and.
Tactic Notation "split_and" "!" := hnf; split_and; split_and?.
Tactic Notation "destruct_or" "?" ident(H) :=
  repeat match type of H with
  | False => destruct H
  | _ \/ _ => destruct H as [H|H]
  end.
Tactic Notation "destruct_or" "!" ident(H) := hnf in H; progress (destruct_or? H).

Tactic Notation "destruct_or" "?" :=
  repeat match goal with H : _ |- _ => progress (destruct_or? H) end.
Tactic Notation "destruct_or" "!" :=
  progress destruct_or?.

Ltac consider X :=
  unfold X in *.

Ltac propositional :=
  repeat match goal with
  | H1: ?x -> _, H2: ?x |- _ =>
      specialize H1 with (1 := H2)
  end.
Ltac assert_pre_and_specialize H :=
  match type of H with
  | ?x -> _ => let Hx := fresh in assert x as Hx; [ | specialize H with (1 := Hx); clear Hx]
  end.

Ltac rewrite_solve :=
  match goal with
  | [ H: _ |- _ ] => solve[rewrite H; try congruence; auto]
  end.
Ltac simplify_tuples :=
  repeat match goal with
  | [ H: (_,_) = (_,_) |- _ ] =>
    apply simple_tuple_inversion in H; destruct H
  end.

Ltac simplify_tupless := simplify_tuples; subst.

Ltac bash_destruct H :=
  repeat destruct_matches_in_hyp H; simpl in H; simplify_tupless; try congruence.

Ltac destruct_and_save H :=
  let H' := fresh H in
  pose proof H as H';
  destruct H'.

Inductive MARK : string -> Type :=
| MkMark : forall s, MARK s.

Tactic Notation "mark" constr(p) :=
  let H := fresh "Mark" in
  learn_hyp p as H.

Tactic Notation "assert_fresh" constr(P) "as" ident(H') :=
  match goal with
  | H : P |- _ => fail 1
  | _ => assert P as H'
  end.
